buildscript {

    repositories {

        google()

        mavenCentral()
    }

    dependencies {

        classpath 'com.android.tools.build:gradle:7.0.4'
    }
}

allprojects {

    repositories {

        google()

        mavenCentral()
    }
}

apply plugin: 'com.android.library'

ext {
    libraryName = 'CodeScanner'
    libraryDescription = 'Code scanner library for Android, based on ZXing'
    libraryVersion = '2.1.0'
    artifact = 'code-scanner'
    developerId = 'yuriy-budiyev'
    developerName = 'Yuriy Budiyev'
    developerEmail = 'yuriy.budiyev@yandex.ru'
    publishedGroupId = 'com.budiyev.android'
    siteUrl = 'https://github.com/yuriy-budiyev/code-scanner'
    gitUrl = 'https://github.com/yuriy-budiyev/code-scanner.git'
    bintrayRepo = 'maven'
    bintrayName = 'code-scanner'
    licenseName = 'MIT License'
    allLicenses = ['MIT']
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 57
        versionName libraryVersion
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api 'com.google.zxing:core:3.3.3'
    api 'androidx.annotation:annotation:1.3.0'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.getDestinationDir()
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
